; ---------------------------------------------------------------------------
; Object 23 - missile that Buzz Bomber throws
; ---------------------------------------------------------------------------

Missile:
		moveq	#0,d0
		move.b	obRoutine(a0),d0
		move.w	Msl_Index(pc,d0.w),d1
		jmp	Msl_Index(pc,d1.w)
; ===========================================================================
Msl_Index:	dc.w Msl_Init-Msl_Index
		dc.w Msl_Main-Msl_Index
		dc.w Msl_Animate-Msl_Index
		dc.w Msl_FromBuzz-Msl_Index
		dc.w Msl_Delete-Msl_Index
		dc.w Msl_FromNewt-Msl_Index

msl_parent = objoff_3C
; ===========================================================================
Msl_Init:
		tst.b	obSubtype(a0)	; was object created by a Newtron?
		bne.s	.newt		; if YEA, branch

		lea	v_player, a3
		move.w	obX(a3), d1
		sub.w	obX(a0), d1
		move.w	obY(a3), d2
		sub.w	obY(a0), d2
		jsr	(CalcAngle).w
		jsr	(CalcSine).w
		muls.w	#$800, d0
		muls.w	#$800, d1
		asr.l	#8, d0
		asr.l	#8, d1
		move.w	d1, obVelX(a0)
		move.w	d0, obVelY(a0)

.newt:
		addq.b 	#2, obRoutine(a0)
		
Msl_Main:	; Routine 0
		subq.w	#1,objoff_32(a0)
		bpl.w	Msl_ChkCancel
		addq.b	#2,obRoutine(a0)
		move.l	#Map_Missile,obMap(a0)
		move.w	#make_art_tile(ArtTile_Buzz_Bomber,1,0),obGfx(a0)
		cmpi.b	#id_CBZ,(v_zone).w		; is zone CBZ?
		bne.s	.NotCBZ	; if not, branch
		move.l	#Map_MissileCBZ,obMap(a0)
		move.w	#make_art_tile(ArtTile_CBZBuzz_Bomber,0,0),obGfx(a0)
.NotCBZ:
		move.b	#4,obRender(a0)
		move.b	#3,obPriority(a0)
		move.b	#8,obActWid(a0)
		andi.b	#3,obStatus(a0)
		cmpi.b	#id_CBZ,(v_zone).w		; is zone CBZ?
		beq.s	.wandastart	; if not, branch
		move.w	#sfx_Bomb,d0
		jsr	(PlaySound_Special).w		; play breaking enemy sound
		move.w  #$15, v_screenshaketime.w

		pcm 	dDicks
		bra.s	.wandaend
.wandastart:
		move.w	#sfx_FCBlip,d0
		jsr	(PlaySound_Special).w		; play Blip sound
.wandaend:
		tst.b	obSubtype(a0)	; was object created by a Newtron?
		beq.s	Msl_Animate	; if not, branch

		move.b	#$A,obRoutine(a0) ; run "Msl_FromNewt" routine
		move.b	#$87,obColType(a0)
		move.b	#1,obAnim(a0)
		bra.s	Msl_Animate2
; ===========================================================================

Msl_Animate:	; Routine 2
		bsr.s	Msl_ChkCancel
		beq.s	Msl_ChkCancel.return
		lea	(Ani_Missile).l,a1
		bsr.w	AnimateSprite
		bra.w	DisplaySprite

; ---------------------------------------------------------------------------
; Subroutine to check if the Buzz Bomber which fired the missile has been
; destroyed, and if it has, then cancel the missile
; ---------------------------------------------------------------------------
; ||||||||||||||| S U B R O U T I N E |||||||||||||||||||||||||||||||||||||||


Msl_ChkCancel:
		movea.l	msl_parent(a0),a1
		_cmpi.b	#id_ExplosionItem,obID(a1) ; has Buzz Bomber been destroyed?
	if FixBugs
		; This adds a return value so that we know if the object has
		; been freed.
		bne.s	.return
		bsr.s	Msl_Delete
		moveq	#0,d0

.return:
	else
		beq.s	Msl_Delete	; if yes, branch
	endif
		rts
; End of function Msl_ChkCancel

; ===========================================================================

Msl_FromBuzz:	; Routine 4
		btst	#7,obStatus(a0)
		bne.s	.explode
		move.b	#$87,obColType(a0)
		move.b	#1,obAnim(a0)
		bsr.w	SpeedToPos

	if FixBugs=0
		; Object should not call DisplaySprite and DeleteObject on
		; the same frame, or else cause a null-pointer dereference.
		lea	(Ani_Missile).l,a1
		bsr.w	AnimateSprite
		bsr.w	DisplaySprite
	endif

		move.w	(v_limitbtm2).w,d0
		addi.w	#$E0,d0
		cmp.w	obY(a0),d0	; has object moved below the level boundary?
		blo.s	Msl_Delete	; if yes, branch

	if FixBugs
		lea	(Ani_Missile).l,a1
		bsr.w	AnimateSprite
		bra.w	DisplaySprite
	else
		rts
	endif
; ===========================================================================

.explode:
;		_move.b	#id_MissileDissolve,obID(a0) ; change object to an explosion (Obj24)
;		move.b	#0,obRoutine(a0)
;		bra.w	MissileDissolve
; ===========================================================================

Msl_Delete:	; Routine 6
		bsr.w	DeleteObject
		rts
; ===========================================================================

Msl_FromNewt:	; Routine 8
		tst.b	obRender(a0)
		bpl.s	Msl_Delete
		bsr.w	SpeedToPos

Msl_Animate2:
		lea	(Ani_Missile).l,a1
		bsr.w	AnimateSprite
		bsr.w	DisplaySprite
		rts
